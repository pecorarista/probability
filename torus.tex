\documentclass{standalone}
% https://tex.stackexchange.com/questions/624264/modular-transformation-in-tikz
\usepackage{luamplib}
\begin{document}
\mplibtextextlabel{enable}
\begin{mplibcode}
beginfig(1);
    rgbcolor sred;
    sred = (1.0, 0.0, 0.2);
    rgbcolor sblue;
    sblue = (0.0, 0.584, 0.851);

    pickup pencircle scaled .3bp;
    path circum, curvefront, curveback, shadowback, shadowfront;
    circum = fullcircle rotated 45 xscaled 89 yscaled 55;
    curvefront = subpath (4.1, 7.9) of fullcircle xscaled 45 yscaled 26 shifted 7 up;
    curveback = subpath (0, 4) of fullcircle xscaled 45 yscaled 26 shifted 3 down
        cutbefore subpath (2, 4) of curvefront cutafter subpath (0, 2) of curvefront;
    shadowback  = buildcycle(point 1 of curveback {direction 1 of curveback} .. point 1/16 of curvefront, curvefront, reverse curveback);
    shadowfront = buildcycle(circum scaled 33/32 rotated 4 shifted (-2, 2), reverse circum);

    path ring, loop;

    ring = circum xscaled 0.8 yscaled 0.65 shifted 7.5 up;
    loop = reverse fullcircle rotated 90 xscaled 23.25 yscaled 14 rotated 47;
    loop := loop shifted (point 4 of circum - point 6 of loop);

    fill shadowback withcolor 7/8;
    fill shadowfront withcolor 7/8;
    draw circum;
    draw curvefront;
    draw curveback;

    interim ahangle := 30;
    interim ahlength := 3;

    drawoptions(withcolor 7/8 sred dashed evenly);
    draw subpath (2, 6) of loop;

    drawoptions(withcolor sred);
    drawarrow subpath (2, 0.5) of loop;
    draw subpath (6, 8) of loop;
    label.lft("$\scriptstyle \theta_1$", point 0 of loop) withcolor sred;
    drawoptions(withpen pencircle scaled 2 withcolor white);
    draw subpath (3.6, 4.3) of ring;

    drawoptions(withcolor sblue);
    drawarrow subpath (-2, 5.8) of ring;
    label.bot("$\scriptstyle \theta_2$", point 6 of ring) withcolor sblue;

endfig;
\end{mplibcode}
\end{document}
